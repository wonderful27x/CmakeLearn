# messageConfig.cmake
# -------------------
#
# message cmake module.
# This module sets the following variables in your project:
#
# ::
#
#   message_FOUND - true if message found on the system
#   message_VERSION - message version in format Major.Minor.Release
#
#
# Exported targets:
#
# ::
#
# If message is found, this module defines the following :prop_tgt:`IMPORTED`
# targets. ::
#   message::message-shared - the main message shared library with header & defs attached.
#   message::message-static - the main message static library with header & defs attached.
#   message::hello-world_wDSO - the hello-world program for the message, linked
#                               against the dynamic shared object.
#   message::hello-world_wAR - the hello-world program for the message, linked
#                               against the static archive.
#
#
# Suggested usage:
#
# ::
#
#   find_package(message)
#   find_package(message 1.1.7 CONFIG REQUIRED)
#
#
# The following variables can be set to guide the search for this package:
#
# ::
#
#   message_DIR - CMake variable, set to directory containing this Config file
#   CMAKE_PREFIX_PATH - CMake variable, set to root directory of this package
#   PATH - environment variable, set to bin directory of this package
#   CMAKE_DISABLE_FIND_PACKAGE_message - CMake variable, disables
#       find_package(message) perhaps to force internal build

@PACKAGE_INIT@

# 包括为目标自动生成的导出文件: messageTargets.cmake
include("${CMAKE_CURRENT_LIST_DIR}/messageTargets.cmake")
# 检查静态库和动态库，以及两个“Hello, World”可执行文件
check_required_components(
  "message-shared"
  "message-static"
  "message-hello-world_wDSO"
  "message-hello-world_wAR"
  )

# Remove dependency on UUID if on Windows
# 检查目标PkgConfig::UUID是否存在。如果没有，我们再次搜索UUID库(只在非Windows操作系统下有效):
if(NOT WIN32)
  if(NOT TARGET PkgConfig::UUID)
    find_package(PkgConfig REQUIRED QUIET)
    pkg_search_module(UUID REQUIRED uuid IMPORTED_TARGET)
  endif()
endif()
